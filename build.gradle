plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE' apply false
}

ext {
    // Project constants
    github_org = 'Discord4J'
    project_name = 'stores'
    artifact_group = 'com.discord4j'
    project_version = '3.0.0-SNAPSHOT'
    project_description = 'Java interface for the Discord API'
    project_jdk = '1.8'

    // Dependencies
    reactor_bom_version = 'Californium-M1'
    autoservice_version = '1.0-rc4'
    commons_codec_version = '1.10'
    jsr305_version = '3.0.2'
    junit_version = '4.12'
    logback_version = '1.3.0-alpha4'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group = artifact_group
    version = project_version
    description = project_description

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    dependencyManagement {
        imports {
            mavenBom "io.projectreactor:reactor-bom:$reactor_bom_version"
        }
    }

    repositories {
        jcenter()
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://jitpack.io' }
    }

    task downloadDependencies() {
        description 'Download all dependencies to the Gradle cache'
        doLast {
            configurations.findAll { it.canBeResolved }.files
        }
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
        options.addStringOption('encoding', 'UTF-8')
        options.tags = ["apiNote:a:API Note:",
                        "implSpec:a:Implementation Requirements:",
                        "implNote:a:Implementation Note:"]
        options.windowTitle = "$rootProject.name ($version)"
        options.links = [
            "https://docs.oracle.com/javase/8/docs/api/",
            "https://docs.oracle.com/javaee/7/api/",
            "http://www.reactive-streams.org/reactive-streams-1.0.2-javadoc/",
            "http://projectreactor.io/docs/core/snapshot/api/",
//            "http://www.javadoc.io/doc/net.openhft/chronicle-map/3.14.5",
//            "https://lettuce.io/core/release/api/"
        ]
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
        publications = ['maven']
        dryRun = false
        publish = true
        pkg {
            userOrg = 'austinv11'
            repo = 'maven'
            name = 'Stores'
            websiteUrl = 'https://discord4j.com'
            issueTrackerUrl = 'https://github.com/Discord4J/Stores/issues'
            vcsUrl = 'https://github.com/Discord4J/Stores.git'
            licenses = ['LGPL-3.0']
            labels = ['discord', 'discord4j', 'd4j', 'reactor', 'nebula']
            publicDownloadNumbers = true
            attributes = [:]
            version {
                name = project.version
                vcsTag = project.version
                gpg {
                    sign = true
                }
                if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                    def sonatypeUsername = project.property('sonatypeUsername')
                    def sonatypePassword = project.property('sonatypePassword')
                    mavenCentralSync {
                        user = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    archivesBaseName = 'stores-' + project.name

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifactId archivesBaseName
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

task aggregateJavadoc(type: Javadoc,
    description: "Generate javadocs from all child projects as if it was a single project",
    group: JavaBasePlugin.DOCUMENTATION_GROUP) {

    destinationDir file("build/docs/aggregateJavadoc")

    subprojects.each { project ->
        project.tasks.withType(Javadoc.class).each { doc ->
            source += doc.source
            classpath += doc.classpath
            excludes += doc.excludes
            includes += doc.includes
        }
    }
}

task aggregateJavadocJar(type: Jar, dependsOn: aggregateJavadoc) {
    classifier = 'javadoc'
    from 'build/docs/aggregateJavadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact aggregateJavadocJar
        }
    }
}

bintrayUpload.dependsOn build

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-4.7-all.zip'
}
